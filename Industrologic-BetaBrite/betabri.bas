100 REM  ------------------------------------------------------------
110 REM  BETABRI.BAS
120 REM  Demonstrates how to control the Adaptive Micro Systems, Inc.
130 REM  Beta-Brite Model 1040 "Electronic Color Message Display"
140 REM  using the EZ KEY II network protocol.
150 REM  This program demonstrates the most likely and useful features
160 REM  of the display and does not include all of the commands. Refer
170 REM  to the command protocol documentation for additional commands.
180 REM
190 REM  Transmission speed: 1200, 2400, 4800 or 9600 baud
200 REM  (The display determines the speed from NULL characters sent)
210 REM  7 data bits, 1 start bits, 2 stop bits, even parity
220 REM  (delays between bytes cannot exceed 1 second or command will time out)
230 REM
240 REM  Transmissions are in the following format:
250 REM  <NULL> 00 Hex (5 to 20 of them so display can determine baud rate)
260 REM  <SOH>  01 Hex
270 REM  Type Code "Z" for "broadcast" to all displays on network
280 REM  Address Field "00" display add. "00" to "FF", (don't care if "broadcast")
290 REM  <STX>  02 Hex
300 REM  Command Code "A"=write and display text file, "E"=write special function
310 REM  (These are the only ones used in this program but there are many others.)
320 REM  Data Field (many different things can go here)
330 REM  <EOT>  04 Hex
340 REM
350 REM  NOTES:
360 REM  THE SPECIAL FUNCTIONS (LIKE THE TIME, DATE, AND DISPLAY MODES)
370 REM  APPEAR TO BE PERSISTANT FOR AS LONG AS THE DISPLAY IS POWERED
380 REM  ON OR IS HOLDING ITS MEMORY, AND MAY NEED TO BE RESET TO THEIR
390 REM  POWER ON STATE IF THEY ARE NO LONGER DESIRED.
400 REM
410 REM  MESSAGES OVER A CERTAIN LENGTH, (DEPENDING ON HOW SOME OF THE
420 REM  MODES ARE SET) WILL CAUSE THE DISPLAY TO SCROLL IF IN SCROLLING
430 REM  MODE OR DISPLAY THE NEXT GROUP OF CHARACTERS IF IN HOLDING MODE.
440 REM
490 REM  ------------------------------------------------------------------
500 OPEN "COM1:2400,E,7,2,CS,DS,CD" AS 1
505 GOSUB 2500:REM CLEAR MEMORY CONFIGURATION AND RESET
510 GOSUB 5000:REM CREATE SOME CONTROL CODE STRINGS FOR LATER USE
520 GOSUB 3000:REM SET TIME
530 GOSUB 3400:REM SET TIME DISPLAY STYLE
540 GOSUB 3100:REM SET DATE
550 GOSUB 3200:REM SET DAY OF WEEK
560 GOSUB 1000:REM DISPLAY MESSAGES
570 CLOSE
580 END
590 REM ----------
1000 DISPMODE$="b"
1010 REM EXAMPLE WITH CONTROL STRINGS
1020 TEXT$=GREEN$+FLASHON$+"H"+FLASHOFF$+"OLD "+YELLOW$+WIDEON$+"WIDE "+WIDEOFF$
1030 TEXT$=TEXT$+DOUBLESTROKEON$+"DBLSTK "+DOUBLESTROKEOFF$
1040 TEXT$=TEXT$+DOUBLEWIDEON$+"DBLWIDE "+DOUBLEWIDEOFF$
1050 GOSUB 2000:REM WRITE MESSAGE
1060 T=TIMER
1070 IF TIMER<T+5 THEN 1070
1080 REM -----
1100 DISPMODE$="n":SPECMODE$="0":REM EXAMPLE WITH SPECIAL MODE
1110 TEXT$=YELLOW$+"TWINKLE"
1120 GOSUB 2000:REM WRITE MESSAGE
1130 T=TIMER
1140 IF TIMER<T+1 THEN 1140
1150 REM -----
1200 DISPMODE$="n":SPECMODE$="2":REM EXAMPLE WITH SPECIAL MODE
1210 TEXT$=BROWN$+"SNOW"
1220 GOSUB 2000:REM WRITE MESSAGE
1230 T=TIMER
1240 IF TIMER<T+2 THEN 1240
1250 REM -----
1300 DISPMODE$="n":SPECMODE$="6":REM EXAMPLE WITH SPECIAL MODE
1310 TEXT$=RED$+"!! SPRAY !!"
1320 GOSUB 2000:REM WRITE MESSAGE
1330 T=TIMER
1340 IF TIMER<T+2.5 THEN 1340
1350 REM -----
1400 DISPMODE$="a"
1410 TEXT$=MIXEDCOLOR$+SPEED1$+BETADOW$+"  "+RAINBOW1$+BETADATE$+"  "+AUTOCOLOR$+BETATIME$
1420 GOSUB 2000:REM WRITE MESSAGE
1430 T=TIMER
1440 IF TIMER<T+6 THEN 1440
1450 REM -----
1500 DISPMODE$="b"
1510 TEXT$=""
1520 GOSUB 2000
1550 RETURN
1540 REM ----------
1820 REM  WRITE MSG FILE "A" AND DISPLAY IT, LINE NUMBER "1"
1830 REM  ENTER WITH THE FOLLOWING STRINGS DEFINED:
1840 REM
1850 REM  DISPMODE$ - DISPLAY MODE
1860 REM  "a"=rotate,"b"=hold,"c"=flash,"d"=n/a (reserved),
1870 REM  "e"=roll up,"f"=roll down,"g"=roll left,"h"=roll right,"i"=wipe up,
1880 REM  "j"=wipe down,"k"=wipe left,"l"=wipe right,"m"=scroll,
1890 REM  "n"=special (add byte below to specify which),"o"=auto,"p"=roll in,
1900 REM  "q"=roll out,"r"=wipe in,"s"=wipe out,"t"=compressed rotate.
1910 REM
1920 REM  SPECMODE$ - SPECIAL MODE
1930 REM  (follow "n" above with one of these)
1940 REM  "0"=twinkle,"1"=sparkle,"2"=snow,"3"=interlock,"4"=switch,"5"=slide,
1950 REM  "6"=spray,"7"=starbursts,"8"=welcome,"9"=slot machine
1960 REM
1970 REM  TEXT$ - TEXT TO BE DISPLAYED, INCLUDING CONTROL STRINGS
1980 REM
1990 REM  "A" IS WRITE/DISPLAY TEXT, "A" IS MESSAGE ID "A", SPACE IS MID LINE?
2000 WRMSGPREFIX$=CHR$(1)+"Z00"+CHR$(2)+"AA"+CHR$(27)+" "
2010 GOSUB 4000:REM LET DISPLAY DETERMINE BAUD RATE
2020 PRINT #1,WRMSGPREFIX$;:REM SEND WRITE MESSAGE COMMAND PREFIX
2030 PRINT #1, DISPMODE$;:REM SET DISPLAY MODE
2040 IF DISPMODE$="n" THEN PRINT #1, SPECMODE$;:REM SET SPECIAL MODE
2050 PRINT #1,TEXT$;:REM DISPLAY TEXT
2060 PRINT #1,CHR$(4);:REM COMMAND SUFFIX, CAN END ON EOT
2070 RETURN
2080 REM ----------
2500 GOSUB 4000:REM GET DISPLAY'S ATTENTION AND LET IT DETERMINE BAUD RATE
2510 PRINT #1,CHR$(1)+"Z00"+CHR$(2)+"E$"+CHR$(4);:REM CLEAR MEMORY CONFIGURATION, RESET
2520 RETURN
2530 REM ----------
3000 GOSUB 4000:REM GET DISPLAY'S ATTENTION AND LET IT DETERMINE BAUD RATE
3010 HHMM$=LEFT$(TIME$,2)+MID$(TIME$,4,2):REM GET CURRENT TIME FROM COMPUTER
3020 PRINT #1,CHR$(1)+"Z00"+CHR$(2)+"E "+HHMM$+CHR$(4);:REM SET TIME AS HHMM
3030 RETURN
3040 REM ----------
3100 GOSUB 4000:REM GET DISPLAY'S ATTENTION AND LET IT DETERMINE BAUD RATE
3110 MMDDYY$=LEFT$(DATE$,2)+MID$(DATE$,4,2)+RIGHT$(DATE$,2):REM GET CURRENT DATE FROM COMPUTER
3120 PRINT #1,CHR$(1)+"Z"+"00"+CHR$(2)+"E;"+MMDDYY$+CHR$(4);:REM SET DATE MMDDYY
3130 RETURN
3140 REM ----------
3200 GOSUB 4000:REM GET DISPLAY'S ATTENTION AND LET IT DETERMINE BAUD RATE
3210 MONTH=VAL(LEFT$(DATE$,2))
3220 DAY=VAL(MID$(DATE$,4,2))
3230 YEAR=VAL(MID$(DATE$,7,4))
3240 IF (MONTH<3) THEN Y=YEAR-1:M=MONTH+12:D=DAY
3250 IF MONTH>=3 THEN Y=YEAR:M=MONTH:D=DAY
3260 DOW=((13*M+3)\5+D+Y+Y\4-Y\100+Y\400+1) MOD 7
3270 DOW$=RIGHT$(STR$(DOW+1),1):REM CREATES DOW = 0-6
3280 PRINT #1,CHR$(1)+"Z00"+CHR$(2)+"E&"+DOW$+CHR$(4);:REM SET DOW, S-S="1"-"7"
3290 RETURN
3300 REM ----------
3400 GOSUB 4000:REM GET DISPLAY'S ATTENTION AND LET IT DETERMINE BAUD RATE
3410 PRINT #1,CHR$(1)+"Z00"+CHR$(2)+"E'S"+CHR$(4);:REM SET TIME STYLE, S OR M
3420 RETURN
3430 REM ----------
4000 FOR A=1 TO 20:PRINT #1,CHR$(0);:NEXT A:REM LET DISPLAY DETERMINE BAUD RATE
4010 RETURN
4020 REM ----------
4940 REM  THE FOLLOWING ARE "CONTROL CODES" THAT CAN BE EMBEDDED IN THE
4950 REM  TEXT AND CAN MODIFY THE APPEARANCE OF THE TEXT, HOWEVER, NOT
4960 REM  ALL OF THEM CAN BE USED WITHIN THE SAME MESSAGE.
4970 REM  FOR EXAMPLE, "FIXED WIDTH" WILL FIX ALL CHARACTERS IN MESSAGE,
4990 REM
5000 RED$=CHR$(28)+"1":REM COLORS
5010 GREEN$=CHR$(28)+"2"
5020 AMBER$=CHR$(28)+"3"
5030 DIMRED$=CHR$(28)+"4"
5040 DIMGREEN$=CHR$(28)+"5"
5050 BROWN$=CHR$(28)+"6"
5060 ORANGE$=CHR$(28)+"7"
5070 YELLOW$=CHR$(28)+"8"
5080 RAINBOW1$=CHR$(28)+"9"
5090 RAINBOW2$=CHR$(28)+"A"
5100 MIXEDCOLOR$=CHR$(28)+"B"
5110 AUTOCOLOR$=CHR$(28)+"C"
5190 REM
5200 SPEED1$=CHR$(21):REM SPEED 1 (SLOWEST)
5210 SPEED2$=CHR$(22):REM SPEED 2
5220 SPEED3$=CHR$(23):REM SPEED 3
5230 SPEED4$=CHR$(24):REM SPEED 4 (DEFAULT)
5240 SPEED5$=CHR$(25):REM SPEED 5 (FASTEST)
5250 NOHOLD$=CHR$(9):REM NO HOLD TIME
5290 REM
5300 WIDEON$=CHR$(18):REM WIDE CHARACTERS ON
5310 WIDEOFF$=CHR$(17):REM WIDE CHARACTERS OFF
5320 PROP$=CHR$(30)+"0":REM PROPORTIONAL CHARACTERS
5330 FIXED$=CHR$(30)+"1":REM FIXED WIDTH CHARACTERS (LEFT JUSTIFIED!)
5340 FLASHOFF$=CHR$(7)+"0":REM CHARACTER FLASH OFF
5350 FLASHON$=CHR$(7)+"1":REM CHARACTER FLASH ON
5390 REM
5400 FONT1$=CHR$(26)+"3":REM CHARACTER SET 7 HIGH STANDARD (DEFAULT)
5420 FONT2$=CHR$(26)+"5":REM CHARACTER SET 7 HIGH FANCY
5430 FONT3$=CHR$(26)+"1":REM CHARACTER SET 5 HIGH STANDARD
5490 REM
5500 DOUBLESTROKEON$=CHR$(29)+"01":REM DOUBLE STROKE CHARACTERS ON
5510 DOUBLESTROKEOFF$=CHR$(29)+"00":REM DOUBLE STROKE CHARACTERS OFF
5520 DOUBLEWIDEON$=CHR$(29)+"11"
5530 DOUBLEWIDEOFF$=CHR$(29)+"10"
5580 FIXEDON$=CHR$(29)+"41"
5590 FIXEDOFF$=CHR$(29)+"40"
5690 REM
5700 BETATIME$=CHR$(19):REM DISPLAY TIME IN FORMAT SELECTED
5710 BETADATE$=CHR$(11)+"0":REM DISPLAY DATE AS MM/DD/YY
5720 BETADOW$=CHR$(11)+"9":REM DISPLAY DAY OF WEEK
5730 RETURN

