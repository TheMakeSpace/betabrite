10 REM  ------------------------------------------------------------
20 REM  BETATIME.BAS
30 REM  DISPLAY TIME ON BETABRITE DISPLAY
40 REM  USES A "STRING" FOR TIME SO DISPLAY DOES NOT BLANK
50 REM  (BUT, STRING COMMAND TAKES LONGER THAN 1 SECOND TO RESPOND)
60 REM -------------------------------------------------------------
70 OPEN "COM1:4800,E,7,2,CS,DS,CD" AS 1
80 REM --- INITIALIZE MEMORY FOR "STRING" COMMAND
100 FOR A=1 TO 10:PRINT #1,CHR$(0);:NEXT A:REM LET DISPLAY DETERMINE BAUD RATE
110 PRINT #1,CHR$(1)+"Z00"+CHR$(2);:REM COMMON PREFIX - BROADCAST
120 PRINT #1,"E$";:REM WRITE MEMORY CONFIGURATION
130 PRINT #1,+"AAU";:REM FILE "A", TEXT FILE, UNLOCKED
140 PRINT #1,"0400FF00";:REM 0400=1024 BYTES, FF=ALWAYS ON, 00=N/A
150 PRINT #1,"1BL";:REM FILE "B", STRING FILE, LOCKED
160 PRINT #1,"007D0000";:REM 007D=125 BYTES (MAX), 0000=N/A
170 PRINT #1,+CHR$(4);:REM EOT
180 REM --- INTIALIZE STRING THAT WILL CONTAIN THE TIME
200 FOR A=1 TO 10:PRINT #1,CHR$(0);:NEXT A:REM LET DISPLAY DETERMINE BAUD RATE
210 PRINT #1,CHR$(1)+"Z00"+CHR$(2);:REM COMMON PREFIX - BROADCAST
220 PRINT #1,"G1";:REM WRITE STRING FILE "1"
230 PRINT #1,"TIME":REM PLACEHOLDER TEXT 
240 PRINT #1,CHR$(4);:REM EOT (FOR NOW WRITE NOTHING INTO STRING)
250 REM --- SEND INITIAL MESSAGE WITHOUT TIME EMBEDDED
300 FOR A=1 TO 10:PRINT #1,CHR$(0);:NEXT A:REM LET DISPLAY DETERMINE BAUD RATE
310 PRINT #1,CHR$(1)+"Z00"+CHR$(2);:REM COMMON PREFIX - BROADCAST
320 PRINT #1,"AA"+CHR$(27)+" b";:REM MESSAGE ON FIRST LINE, MODE="HOLD"
330 REM PRINT #1,CHR$(30)+"1";:REM FIXED WIDTH LEFT JUSTIFIED
340 PRINT #1,CHR$(28)+"1";:REM COLOR RED
350 PRINT #1,"";:REM HEADER TEXT, ACTUALLY A BLANK PLACEHOLDER
360 PRINT #1,CHR$(16)+"1";:REM CALL STRING "1"
370 PRINT #1,"";:REM TRAILER TEXT, ACTUALLY A BLANK PLACEHOLDER
380 PRINT #1,CHR$(4);:REM EOT
390 REM --- DISPLAY TIME BY FILLING STRING
400 IF RIGHT$(TIME$,1)="0" OR RIGHT$(TIME$,1)="5" THEN 430
410 IN$=INKEY$:IF IN$=CHR$(27) THEN 600:REM EXIT IF ESCAPE KEY
420 GOTO 400
430 FOR A=1 TO 10:PRINT #1,CHR$(0);:NEXT A:REM LET DISPLAY DETERMINE BAUD RATE
440 PRINT #1,CHR$(1)+"Z00"+CHR$(2);:REM COMMON PREFIX - BROADCAST
450 PRINT #1,"G1";:REM WRITE TO STRING "1"
460 PRINT #1,TIME$;:REM WRITE STRING
470 PRINT #1,CHR$(4);:REM EOT
480 T=TIMER
490 IF TIMER<T+.5 THEN 490
500 GOTO 400
510 REM --- CLEAR DISPLAY IF EXITING
600 PRINT #1,CHR$(1)+"Z00"+CHR$(2);:REM COMMON PREFIX - BROADCAST
610 PRINT #1,"AA"+CHR$(27)+" "+"b";:REM MESSAGE ON FIRST LINE, MODE="HOLD"
620 PRINT #1,"";:REM BLANK MESSAGE TO CLEAR DISPLAY
630 PRINT #1,CHR$(4);:REM EOT

